
# Expected to be imported by "anim/_spider.fus"

# Expected vars:
# NS (str), e.g. "spider_"


proc "crawl_crush":
    if: expr: myvar("is_spikeyspider")
    then: goto delay: "crawl_shielding"
    else: goto: "dead"


state "crawling":
    rgraph: $PREFIX NS "crawling"
    hitbox: collmap("stand")
    if() then: goto: "crawl"

state "uncrawling":
    rgraph: $PREFIX NS "uncrawling"
    hitbox: collmap("stand")
    if() then: goto: "stand"

state "crawling_looking_up":
    rgraph: $PREFIX NS "crawling_looking_up"
    hitbox: collmap("stand")
    if() then: goto: "crawl_looking_up"

state "uncrawling_looking_up":
    rgraph: $PREFIX NS "uncrawling_looking_up"
    hitbox: collmap("stand")
    if() then: goto: "looking_up"

state "crawl_looking_up":
    rgraph: $PREFIX NS "crawl_looking_up"
    hitbox: collmap("crawl")
    on "crush": call: "crawl_crush"

    if:
        key: wentdown y
    then:
        call: "crawl_looking_up_spit"

    if:
        key: not isdown d
        coll: all no
            ;;  *\*/*
            ;;  - + -
            ;;  */ \*
            ;; ( )
    then:
        goto delay: "uncrawling_looking_up"

    if: key: wasdown f
    then: goto immediate: "crawl"

    if: key: wasdown b
    then: goto immediate: "crawl"

    if: key: not isdown x
    then: goto: "crawl"

state "crawl_spit":
    rgraph: $PREFIX NS "crawl_spit"
    hitbox: collmap("crawl")
    on "crush": call: "crawl_crush"
    if() then: goto: "crawl"

state "crawl_looking_up_spit":
    rgraph: $PREFIX NS "crawl_looking_up_spit"
    hitbox: collmap("crawl")
    on "crush": call: "crawl_crush"
    if() then: goto: "crawl_looking_up"

state "crawl":
    rgraph: $PREFIX NS "crawl"
    hitbox: collmap("crawl")

    on "crush": call: "crawl_crush"

    # Spit
    if:
        key: wentdown y
    then:
        call: "crawl_spit"

    # Turn into bird
    if:
        expr: myvar("is_birdspider")
        key: wentdown x
        coll: all no
            ;;   .   .
            ;;   *\*/*
            ;; . - + - .
            ;;   */*\*
            ;;   . - .
            ;;   *\*/*
            ;; . - + - .
            ;;   */*\*
            ;;  (.)  .
    then:
        goto: "bird_crawl"
        delay: 1

    # Uncrawl
    if:
        key: not isdown d
        coll: all no
            ;;  *\*/*
            ;;  - + -
            ;;  */ \*
            ;; ( )
    then:
        goto delay: "uncrawling"

    # Crawling
    import "anim/_spider_crawl.state.fus"

    # Roll over a gap
    if:
        expr: myvar("is_rollerspider")
        key: wasdown f
        coll: all no
            ;;     \*
            ;; (.)  .
    then:
        goto: "roller_stand"
        move: 1 0
        turn
        rot: 1
        delay: $GET_INT ROLLER_STAND_DELAY

    # Roll into a corner
    if:
        expr: myvar("is_rollerspider")
        key: wasdown f
        coll: all yes
            ;;    +
            ;;     \
            ;; (.)  +
    then:
        goto: "crawl_step"
        move: 1 0
        rot: 2
        delay: 2

    # Turn
    if:
        key: wasdown b
        key: not isdown f
    then:
        move: 1 0
        turn
        goto: "crawl_turn"
        delay: 2

    # Look up
    if:
        expr: myvar("is_aimerspider")
        key: not isdown f
        key: wasdown x
    then:
        goto: "crawl_look_up"

state "crawl_look_up":
    rgraph: $PREFIX NS "crawl_look_up"
    hitbox: collmap("crawl")
    on "crush": call: "crawl_crush"
    if() then: goto: "crawl_looking_up"

state "crawl_step_down":
    rgraph: $PREFIX NS "crawl_step"
    hitbox:
        ;;    .   .
        ;;    *\*/*
        ;;  . -(.)  .
    on "crush": call: "crawl_crush"

    if:
        key: wentdown y
    then:
        call: "crawl_spit"

    if() then: goto: "crawl"

state "crawl_step":
    # If you make changes to this, you'll probably want to make same
    # changes to crawl_turn
    rgraph: $PREFIX NS "crawl_step"
    hitbox:
        ;;    .   .
        ;;      */*
        ;;     (.)  .
    on "crush": call: "crawl_crush"

    if:
        key: wentdown y
    then:
        call: "crawl_spit"

    if() then: goto: "crawl"

state "crawl_turn":
    # Like crawl_step, but without the extended hitbox
    # (so you can turn around in-place while crouching, without exposing yourself
    # to being crushed)
    rgraph: $PREFIX NS "crawl_turn"
    hitbox: collmap("crawl")
    on "crush": call: "crawl_crush"

    if() then: goto immediate: "crawl_step"

state "crawl_shielding":
    rgraph: $PREFIX NS "crawl_shielding"
    hitbox: collmap("crawl")
    on "crush": # Do nothing
    if:
    then: goto: "crawl"
