
# Expected to be imported by "anim/_spider.fus"

# Expected vars:
# NS (str), e.g. "spider_"

crawling:
    rgraph: $PREFIX NS "crawling"
    hitbox: collmap("stand")
    if() then: goto: crawl

uncrawling:
    rgraph: $PREFIX NS "uncrawling"
    hitbox: collmap("stand")
    if() then: goto: stand

crawling_looking_up:
    rgraph: $PREFIX NS "crawling_looking_up"
    hitbox: collmap("stand")
    if() then: goto: crawl_looking_up

uncrawling_looking_up:
    rgraph: $PREFIX NS "uncrawling_looking_up"
    hitbox: collmap("stand")
    if() then: goto: looking_up

crawl_looking_up:
    rgraph: $PREFIX NS "crawl_looking_up"
    hitbox: collmap("crawl")
    $IF SPIDER_SPIKEY: on "crush": goto delay: crawl_shielding

    if:
        key: wentdown y
    then:
        call: crawl_looking_up_spit

    if:
        key: not isdown d
        coll: all no
            ;;  *\*/*
            ;;  - + -
            ;;  */ \*
            ;; ( )
    then:
        goto delay: uncrawling_looking_up

    if: key: isdown f
    then: goto immediate: crawl

    if: key: isdown b
    then: goto immediate: crawl

    if: key: not isdown x
    then: goto: crawl

crawl_spit:
    rgraph: $PREFIX NS "crawl_spit"
    hitbox: collmap("crawl")
    $IF SPIDER_SPIKEY: on "crush": goto delay: crawl_shielding
    if() then: goto: crawl

crawl_looking_up_spit:
    rgraph: $PREFIX NS "crawl_looking_up_spit"
    hitbox: collmap("crawl")
    $IF SPIDER_SPIKEY: on "crush": goto delay: crawl_shielding
    if() then: goto: crawl_looking_up

crawl:
    rgraph: $PREFIX NS "crawl"
    hitbox: collmap("crawl")

    $IF SPIDER_SPIKEY: on "crush": goto delay: crawl_shielding

    # Spit
    if:
        key: wentdown y
    then:
        call: crawl_spit

    # Turn into roller
    $IF SPIDER_ROLLER:
        if:
            key: wentdown x
        then:
            goto: roller_stand
            delay: $GET_INT ROLLER_STAND_DELAY

    # Turn into bird
    $IF SPIDER_BIRD:
        if:
            key: wentdown x
            coll: all no
                ;;   .   .
                ;;   *\*/*
                ;; . - + - .
                ;;   */*\*
                ;;   . - .
                ;;   *\*/*
                ;; . - + - .
                ;;   */*\*
                ;;  (.)  .
        then:
            goto: bird_crawl
            delay: 1

    # Uncrawl
    if:
        key: not isdown d
        coll: all no
            ;;  *\*/*
            ;;  - + -
            ;;  */ \*
            ;; ( )
    then:
        goto delay: uncrawling

    # Crawling
    import "anim/_spider_crawl.anim.fus"

    # Roll over a gap
    $IF SPIDER_ROLLER:
        if:
            key: isdown f
            coll: all no
                ;;     \*
                ;; (.)  .
        then:
            goto: roller_stand
            move: 1 0
            turn
            rot: 1
            delay: $GET_INT ROLLER_STAND_DELAY

    # Roll into a corner
    $IF SPIDER_ROLLER:
        if:
            key: isdown f
            coll: all yes
                ;;    +
                ;;     \
                ;; (.)  +
        then:
            goto: crawl_step
            move: 1 0
            rot: 2
            delay: 2

    # Turn
    if:
        key: isdown b
    then:
        move: 1 0
        turn
        goto: crawl_turn
        delay: 2

    # Look up
    $IF SPIDER_AIMER:
        if:
            key: not isdown f
            key: isdown x
        then:
            goto: crawl_look_up

crawl_look_up:
    rgraph: $PREFIX NS "crawl_look_up"
    hitbox: collmap("crawl")
    $IF SPIDER_SPIKEY: on "crush": goto delay: crawl_shielding
    if() then: goto: crawl_looking_up

crawl_step_down:
    rgraph: $PREFIX NS "crawl_step"
    hitbox:
        ;;    .   .
        ;;    *\*/*
        ;;  . -(.)  .
    $IF SPIDER_SPIKEY: on "crush": goto delay: crawl_shielding

    if:
        key: wentdown y
    then:
        call: crawl_spit

    $IF SPIDER_ROLLER:
        if:
            key: wentdown x
        then:
            goto: roller_stand
            delay: $GET_INT ROLLER_STAND_DELAY

    if() then: goto: crawl

crawl_step:
    # If you make changes to this, you'll probably want to make same
    # changes to crawl_turn
    rgraph: $PREFIX NS "crawl_step"
    hitbox:
        ;;    .   .
        ;;      */*
        ;;     (.)  .
    $IF SPIDER_SPIKEY: on "crush": goto delay: crawl_shielding

    if:
        key: wentdown y
    then:
        call: crawl_spit

    $IF SPIDER_ROLLER:
        if:
            key: wentdown x
        then:
            goto: roller_stand
            delay: $GET_INT ROLLER_STAND_DELAY

    if() then: goto: crawl

crawl_turn:
    # Like crawl_step, but without the extended hitbox
    # (so you can turn around in-place while crouching, without exposing yourself
    # to being crushed)
    rgraph: $PREFIX NS "crawl_turn"
    hitbox: collmap("crawl")
    $IF SPIDER_SPIKEY: on "crush": goto delay: crawl_shielding

    if() then: goto immediate: crawl_step

$IF SPIDER_SPIKEY: crawl_shielding:
    rgraph: $PREFIX NS "crawl_shielding"
    # (No hitbox: we are invincible)
    if:
    then: goto: crawl
