
collmsgs: "crush"

state "stand":
    rgraph: "big_roller"
    hitbox:
        ;;      .
        ;;      *
        ;;    . - .
        ;;    *\*/*
        ;; ( )  .   .
    on "spikeyspit": goto: "turn"

    if:
        # Grabbing
        key: wasdown u
        coll: all yes
            ;;      .   .   +
            ;;
            ;;    .   .   .
            ;;
            ;; ( )  .   .
        coll: all no
            ;;      .   .   .
            ;;       \*/*\*
            ;;    .   + - .
            ;;         \*
            ;; ( )  .   .
    then:
        move: 4 2
        rot: 3
        goto: "grab"
        delay: 1

    if:
        # Rolling forward (not holding d)
        key: not isdown d
        key: not isdown b
        key: wasdown f
        coll: all yes
            ;; ( )  .   +
    then:
        move: 2 0
        rot: 2
        delay: 1

    if:
        # Spinning around forward (possibly holding d)
        key: not isdown b
        key: wasdown f
        coll: all no
            ;; ( )- + -
            ;;    */*\*
            ;;      -
    then:
        rot: -1
        delay: 1

    if:
        # Rolling forward (possibly holding d)
        key: not isdown b
        key: wasdown f
        coll: all yes
            ;; ( )  .   +
    then:
        move: 2 0
        rot: 2
        delay: 1

    if:
        key: not isdown f
        key: wasdown b
    then:
        turn
        rot: 2
        delay: 1

state "turn":
    # When bumped, turn around
    if:
    then:
        confused: toggle
        goto: "stand"

state "grab":
    rgraph: "big_roller_grab"
    hitbox:
        ;;      .   .   .
        ;;      *\*/*\*
        ;;    . - + - .
        ;;    *\*/*\*
        ;; ( )  .   .

    if:
        key: not isdown u
    then:
        goto: "stand"
        delay: 1
