
# Expected vars:
# NS (str), e.g. "player_"
# PLAYER_AIMER (bool)
# PLAYER_BIRD (bool)
# PLAYER_FLIER (bool)
# PLAYER_SPIKEY (bool)
# PLAYER_ROLLER (bool)
# PLAYER_EYE (bool)

# NOTE: make sure "stand" is the first anim, so any imports
# should go below it

$IF PLAYER_SPIKEY:
    $SET_STR SPIT_FUS "anim/spikeyspit.fus"
$IF !PLAYER_SPIKEY:
    $SET_STR SPIT_FUS "anim/spit.fus"


$SET_INT ROLLER_STAND_DELAY 0

$SET_STR STATE_STEP_UP step
$SET_STR STATE_LAND step
$SET_STR STATE_LAND_UP step


collmsgs: "touch"
on "crush": goto: dead
on "carryable":
    if: not: exists myvar("carrying")
    then:
        set myvar("carrying"): yourvar("carryable")
        set myvar("carrying_rgraph"): yourvar("carryable_rgraph")
        set_label "carrying": myvar("carrying_rgraph")

        as you:
            goto immediate: _collected


proc _spawn_spit:
    if: exists myvar("carrying")
    then:

        # Set spit's "carrying" to player's "carrying"
        as you:
            set myvar("carrying"): yourvar("carrying")
            set myvar("carrying_rgraph"): yourvar("carrying_rgraph")
            set_label "carrying": myvar("carrying_rgraph")

        # Unset player's "carrying"
        set myvar("carrying"): null
        set_label "carrying": null

proc spit:
    spawn: $GET_STR SPIT_FUS "fly" (0 0) 0 f do:
        call: _spawn_spit
    goto delay: spit

proc looking_up_spit:
    spawn: $GET_STR SPIT_FUS "fly" (1 0) 1 f do:
        call: _spawn_spit
    goto delay: looking_up_spit

proc crawl_spit:
    spawn: $GET_STR SPIT_FUS "crouch_fly" (0 0) 0 f do:
        call: _spawn_spit
    goto delay: crawl_spit

proc crawl_looking_up_spit:
    spawn: $GET_STR SPIT_FUS "crouch_fly" (1 0) 1 f do:
        call: _spawn_spit
    goto delay: crawl_looking_up_spit

proc swim_spit:
    spawn: $GET_STR SPIT_FUS "fly" (0 0) 0 f do:
        call: _spawn_spit
    goto delay: swim_spit


collmap "stand":
    ;;   .   .
    ;;   *\*/*
    ;; . - + - .
    ;;   */*\*
    ;;  (.)  .
collmap "crawl":
    ;;     .
    ;;     *
    ;;  (.)  .

stand:
    rgraph: $PREFIX NS "stand"
    hitbox: collmap("stand")

    # Can't stand on nothing
    if:
        coll: any no
            ;;  (+)- +
    then:
        move: 1 0
        goto immediate: start_jump

    # Spit
    if:
        key: wentdown y
    then:
        call: spit

    # Turn into roller
    $IF PLAYER_ROLLER:
        if:
            key: wentdown x
        then:
            goto: roller_stand
            delay: $GET_INT ROLLER_STAND_DELAY

    # Turn into bird
    $IF PLAYER_BIRD:
        if:
            key: wentdown x
            coll: all no
                ;;   .   .
                ;;   *\*/*
                ;; . - + - .
                ;;   */*\*
                ;;   . - .
                ;;   *\*/*
                ;; . - + - .
                ;;   */*\*
                ;;  (.)  .
        then:
            goto: bird_crawl
            delay: 1

    # Blink
    $IF PLAYER_EYE:
        if:
            key: wasdown x
        then:
            goto delay: eye_closing

    # Crawl
    if:
        key: isdown d
    then:
        goto delay: crawling

    # Stepping and jumping
    import "anim/_player_move.state.fus"

    # Turn
    if:
        key: isdown b
    then:
        move: 1 0
        turn
        goto delay: turn

    # Look up
    $IF PLAYER_AIMER:
        if:
            key: isdown x
        then:
            goto: look_up

look_up:
    rgraph: $PREFIX NS "look_up"
    hitbox: collmap("stand")
    if() then: goto: looking_up

looking_up:
    rgraph: $PREFIX NS "looking_up"
    hitbox: collmap("stand")

    if:
        key: wentdown y
    then:
        call: looking_up_spit

    if:
        key: wasdown u
        coll: all no
            ;;    *\*/*
            ;;    - + -
            ;;       \*
            ;;
            ;;
            ;; ( )
    then:
        move: 1 0
        rot: 1
        goto immediate: start_jump

    if: key: isdown f
    then: goto immediate: stand

    if: key: isdown b
    then: goto immediate: stand

    if:
        key: isdown d
    then:
        goto delay: crawling_looking_up

    if: key: not isdown x
    then: goto: stand

spit:
    rgraph: $PREFIX NS "spit"
    hitbox: collmap("stand")
    if() then: goto: stand

looking_up_spit:
    rgraph: $PREFIX NS "looking_up_spit"
    hitbox: collmap("stand")
    if() then: goto: looking_up

start_jump:

    $IF PLAYER_FLIER:
        if:
            key: wasdown x
            coll: all no
                ;;       \*/*
                ;;        + -
                ;;       /*\*
                ;; ( )
        then:
            goto: flier_fly
            move: 2 1
            delay: 2

    if:
        coll: water any yes collmap("stand")
    then:
        goto delay: swim_jump

    if() then:
        goto delay: jump

jump:
    # NOTE: the only "goto: jump" is in "start_jump", which checks whether
    # we're in water, and does "goto: jump" or "goto: swim_jump"
    # accordingly.
    # Therefore, if we're in "jump", then we can safely assume we're not in
    # water without checking.

    unsafe
    rgraph: $PREFIX NS "jump"
    hitbox: collmap("stand")

    # Landing, jumping, crashing
    import "anim/_player_jump.state.fus"

step_down:
    rgraph: $PREFIX NS "step"
    hitbox:
        ;;   .   .   .
        ;;   *\*/*\*/*
        ;; . - + - + - .
        ;;   */*\*/*\*
        ;;   .  (.)  .

    if:
        key: wentdown y
    then:
        call: spit

    $IF PLAYER_ROLLER:
        if:
            key: wentdown x
        then:
            goto: roller_stand
            delay: $GET_INT ROLLER_STAND_DELAY

    if() then: goto: stand

step:
    rgraph: $PREFIX NS "step"
    hitbox: collmap("stand")

    if:
        key: wentdown y
    then:
        call: spit

    $IF PLAYER_ROLLER:
        if:
            key: wentdown x
        then:
            goto: roller_stand
            delay: $GET_INT ROLLER_STAND_DELAY

    if() then: goto: stand

turn:
    rgraph: $PREFIX NS "turn"
    hitbox: collmap("stand")

    if() then: goto immediate: step

sleep:
    rgraph: $PREFIX NS "sleep"
    hitbox: collmap("crawl")
    if: any:
        key: isdown u
        key: isdown d
        key: isdown l
        key: isdown r
    then: goto: crawl

$IF PLAYER_EYE:
    eye_closing:
        rgraph: $PREFIX NS "eye_closing"
        hitbox: collmap("stand")
        if() then: goto: eye_closed
    eye_unclosing:
        rgraph: $PREFIX NS "eye_unclosing"
        hitbox: collmap("stand")
        if() then: goto: stand
    eye_closed:
        rgraph: $PREFIX NS "eye_closed"
        hitbox: collmap("stand")
        if:
            key: not isdown x
        then: goto delay: eye_unclosing

mostly_dead:
    rgraph: "dead_cycle"
    unsafe
    if() then: die mostly

dead:
    rgraph: "dead_cycle"
    unsafe
    if() then: die

import "anim/_player_crawl.fus"
import "anim/_player_swim.fus"
