
# Expected collmaps:
# * "hitbox"

# Expected vars at runtime:
# * key (str)

vars:
    "value": T

state "init":
    if: any:
        all:
            exists myvar("key")
            expr: == mapvar(myvar("key")) myvar("value")
        all:
            exists myvar("counter")
            expr: mapvar(myvar("counter"))
    then:
        goto immediate: "touched"

    if:
    then:
        goto immediate: "waiting"

state "waiting":
    rgraph: "hotspot_waiting"
    hitbox: collmap("hitbox")

    on "touch":
        if: any:
            not: exists myvar("need_key")
            expr: mapvar(myvar("need_key"))
        then:
            if: exists myvar("key")
            then: set mapvar(myvar("key")): myvar("value")

            if: exists myvar("counter")
            then: inc mapvar(myvar("counter"))

            if: exists myvar("max_cooldown")
            then: set myvar("cooldown"): myvar("max_cooldown")

            goto: "touched"

state "touched":
    rgraph: "hotspot_touched"
    hitbox: collmap("hitbox")

    on "touch":
        # As long as we're being touched, keep our cooldown at max
        if: exists myvar("max_cooldown")
        then: set myvar("cooldown"): myvar("max_cooldown")

    if: exists myvar("cooldown")
    then:
        dec myvar("cooldown")
        if: expr: <= myvar("cooldown") 0
        then:
            if: exists myvar("key")
            then: set mapvar(myvar("key")): F

            if: exists myvar("counter")
            then: dec mapvar(myvar("counter"))

            goto: "waiting"
