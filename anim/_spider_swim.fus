
# Expected to be imported by "anim/_spider.fus"

# Expected vars:
# NS (str), e.g. "spider_"


vars:
    nosave "bubble_toggle": F

state "swim_spit":
    rgraph: $PREFIX NS "swim_spit"
    hitbox: collmap("stand")
    unsafe
    "default"
    if() then: goto: "swim"

state "swim_jump":
    # NOTE: the only goto: "swim_jump" is in "start_jump", which checks
    # whether we're in water, and does goto: "jump" or goto: "swim_jump"
    # accordingly.
    # Therefore, if we're in "swim_jump", then we can safely assume we're in
    # water without checking.

    rgraph: $PREFIX NS "swim_jump"
    hitbox: collmap("stand")
    unsafe

    "spawn_bubbles"
    if:
    then:
        if: expr: myvar("bubble_toggle")
        then:
            spawn: "anim/bubbles.fus" "bubbles" (0 0) 0 f
            set myvar("bubble_toggle"): F
        else:
            set myvar("bubble_toggle"): T
        continue

    "land_up"
    if:
        any:
            key: isdown f
            key: isdown u
            key: isdown d
        any:
            all:
                expr: myvar("is_stickyspider")
                coll: all yes
                    ;;        +
                    ;;
                    ;; ( )  +
            all:
                not: expr: myvar("is_stickyspider")
                coll: all yes
                    ;;        +
                    ;;       /
                    ;; ( )  +
    then:
        move: 1 0
        rot: 1
        goto delay: "step"

    "swim_up"
    if:
        any:
            key: isdown f
            key: isdown u
            key: isdown d
        coll: any yes
            ;;        +
            ;;       /
            ;; ( )
        coll: all no
            ;;    *\*/*
            ;;    - + -
            ;;       \*
            ;;    .
            ;;
            ;; ( )
    then:
        move: 2 1
        rot: 1
        goto immediate: "start_jump"

    "swim_down"
    if:
        any:
            key: isdown f
            key: isdown u
            key: isdown d
        coll: any yes
            ;;       \
            ;;        +
            ;;
            ;; ( )
        coll: all no
            ;;
            ;;       /*
            ;; ( )- + -
            ;;    */*\*
    then:
        move: 0 0
        rot: -1
        goto immediate: "start_jump"

    "swim_forward"
    if:
        any:
            key: isdown f
            key: isdown u
            key: isdown d
        coll: all no
            ;;       \*/*
            ;;        + -
            ;;       /*\*
            ;; ( )
    then:
        move: 1 0
        goto immediate: "start_jump"

    "default"
    if() then:
        goto immediate: "swim"

state "swim":
    rgraph: $PREFIX NS "swim"
    hitbox: collmap("stand")
    unsafe

    "always"
    if:
    then:
        set myvar("bubble_toggle"): T
        continue

    "spit"
    if:
        key: wentdown y
    then:
        call: "swim_spit"

    "land_up"
    if:
        any:
            key: isdown f
            key: isdown u
            key: isdown d
        any:
            all:
                expr: myvar("is_stickyspider")
                coll: all yes
                    ;;        +
                    ;;
                    ;; ( )  +
            all:
                not: expr: myvar("is_stickyspider")
                coll: all yes
                    ;;        +
                    ;;       /
                    ;; ( )  +
    then:
        move: 1 0
        rot: 1
        goto delay: "step"

    "swim_up"
    if:
        any:
            key: isdown f
            key: isdown u
            key: isdown d
        any:
            key: isdown u
            coll: any yes
                ;;        +
                ;;       /
                ;; ( )
        coll: all no
            ;;    *\*/*
            ;;    - + -
            ;;       \*
            ;;    .
            ;;
            ;; ( )
    then:
        move: 2 1
        rot: 1
        goto immediate: "start_jump"

    "swim_down"
    if:
        any:
            key: isdown f
            key: isdown u
            key: isdown d
        any:
            key: isdown d
            coll: any yes
                ;;       \
                ;;        +
                ;;
                ;; ( )
        coll: all no
            ;;
            ;;       /*
            ;; ( )- + -
            ;;    */*\*
    then:
        move: 0 0
        rot: -1
        goto immediate: "start_jump"

    "swim_forward"
    if:
        any:
            key: isdown f
            key: isdown u
            key: isdown d
        coll: all no
            ;;       \*/*
            ;;        + -
            ;;       /*\*
            ;; ( )
    then:
        move: 1 0
        goto immediate: "start_jump"

    "turn"
    if:
        key: isdown b
    then:
        move: 1 0
        turn
        delay: 2

    "land"
    if:
        any:
            all:
                expr: myvar("is_stickyspider")
                coll: all yes
                    ;; (+)  +
            all:
                not: expr: myvar("is_stickyspider")
                coll: all yes
                    ;; (+)- +
    then:
        goto: "stand"
        delay: 2
