
collmap "opening":
    ;;            + - + - + - + - + - +
    ;;           /*\*/*\*/*\*/*\*/*\*/*\
    ;;          + - + - + - + - + - + - +
    ;;         /*\*/*\*/*\*/*\*/*\*/*\*/*\
    ;;        + - + - + - + - + - + - + - +
    ;;       /*\*/*\*/*\*/*\*/*\*/*\*/*\*/*\
    ;;      + - + - + - + - + - + - + - + - +
    ;;     /*\*/*\*/*\*/*\*/*\*/*\*/*\*/*\*/*\
    ;;    + - + - + - + - + - + - + - + - + - +
    ;;   /*\*/*\*/*\*/*\*/*\*/*\*/*\*/*\*/*\*/*\
    ;;  + - + - + - + - + -(+)- + - + - + - + - +
    ;;   \*/*\*/*\*/*\*/*\*/*\*/*\*/*\*/*\*/*\*/
    ;;    + - + - + - + - + - + - + - + - + - +
    ;;     \*/*\*/*\*/*\*/*\*/*\*/*\*/*\*/*\*/
    ;;      + - + - + - + - + - + - + - + - +
    ;;       \*/*\*/*\*/*\*/*\*/*\*/*\*/*\*/
    ;;        + - + - + - + - + - + - + - +
    ;;         \*/*\*/*\*/*\*/*\*/*\*/*\*/
    ;;          + - + - + - + - + - + - +
    ;;           \*/*\*/*\*/*\*/*\*/*\*/
    ;;            + - + - + - + - + - +

collmap "looking":
    ;;                          + - +
    ;;                         /*\*/*\
    ;;                        + - + - +
    ;;                       /*\*/*\*/*\
    ;;                      + - + - + - +
    ;;                     /*\*/*\*/*\*/*\
    ;;                    + - + - + - + - +
    ;;                   /*\*/*\*/*\*/*\*/*\
    ;;                  + -(+)- + - + - + - +
    ;;                   \*/*\*/*\*/*\*/*\*/
    ;;                    + - + - + - + - +

collmap "looking_l": collmap("looking") ^  2
collmap "looking_d": collmap("looking") ^ -2

closed:
    rgraph: "eye_closed"

    if: coll: bodies any yes collmap("opening")
    then: goto delay: opening

opening:
    rgraph: "eye_opening"
    if() then: goto: open

open:
    rgraph: "eye_open"

    # Apparently "if: coll: bodies all no" is not the same as
    # "if: not: coll: bodies any yes"... isn't that a bug?
    # Doesn't it mean we fail De Morgan's Law?
    if: not: coll: bodies any yes collmap("opening")
    then: goto delay: closing

    if:
        coll: bodies any yes collmap("looking")
        not: coll: bodies any yes collmap("looking_l")
        not: coll: bodies any yes collmap("looking_d")
    then: goto delay: looking

    if:
        not: coll: bodies any yes collmap("looking")
        coll: bodies any yes collmap("looking_l")
        not: coll: bodies any yes collmap("looking_d")
    then: goto delay: looking_l

    if:
        not: coll: bodies any yes collmap("looking")
        not: coll: bodies any yes collmap("looking_l")
        coll: bodies any yes collmap("looking_d")
    then: goto delay: looking_d

closing:
    rgraph: "eye_closing"
    if() then: goto: closed

# "anim/_eye_looking.fus":
# Provides states looking, unlooking, look (with suffix indicated
# by LOOK_SUFFIX)
$SET_STR LOOK_SUFFIX ""
import "anim/_eye_looking.fus"
$SET_STR LOOK_SUFFIX "_l"
import "anim/_eye_looking.fus"
$SET_STR LOOK_SUFFIX "_d"
import "anim/_eye_looking.fus"
