
# Expected variables:
# RGRAPH_NS (str): e.g. "spiky_", ""

$SET_INT MAX_AGE 100


proc on_death:

    if: exists myvar("carrying")
    then:
        # This spit is carrying something

        # Decrement a mapvar tracking how many of this thing are
        # being carried
        if: exists myvar("carrying_respawn_mapvar")
        then: dec mapvar(myvar("carrying_respawn_mapvar"))

        # We're no longer carrying anything
        set myvar("carrying"): null


fly:
    rgraph: $PREFIX RGRAPH_NS "spit_fly"
    collmsgs: "spit" $IF SPIKEY("spikeyspit")
    hitbox:
        ;;   .   .
        ;;   *\*/
        ;; . - +   .
        ;;
        ;;  (.)  .

    on "crush": goto: dead
    on "solid": goto: dead

    if:
    then:
        inc myvar("age")
        continue

    if: any:
        expr: > myvar("age") $GET_INT MAX_AGE
        coll: any yes
            ;;   .   .
            ;;   *\*
            ;; .   .   .
            ;;
            ;;  ( )  .
    then:
        goto delay: dead

    if:
        coll: all yes
            ;;   .   .
            ;;      /
            ;; .   .   .
            ;;
            ;;  ( )  .
    then:
        move: 2 1
        rot: 1

    if() then:
        move: 1 0

crouch_fly:
    rgraph: $PREFIX RGRAPH_NS "spit_crouch_fly"
    collmsgs: "spit" $IF SPIKEY("spikeyspit")
    hitbox:
        ;;    .   .
        ;;  */*\
        ;; (.)  .   .

    on "crush": goto: crouch_dead
    on "solid": goto: crouch_dead

    if:
    then:
        inc myvar("age")
        continue

    if: any:
        expr: > myvar("age") $GET_INT MAX_AGE
        coll: any yes
            ;;    .   .
            ;;    *\*
            ;; ( )  .
    then:
        goto delay: crouch_dead

    if:
        coll: all yes
            ;;    .   .
            ;;       /
            ;; ( )  .
    then:
        move: 1 0
        rot: 1

    if() then:
        move: 1 0

dead:
    rgraph: "dead_once"
    if() then:
        call: on_death
        remove

crouch_dead:
    rgraph: "dead_once_crouch"
    if() then:
        call: on_death
        remove
