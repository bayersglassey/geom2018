
on "crush": goto: dead

import "anim/_coin.fus"

appear:
    rgraph: "dead_once_reversed"
    if() then: goto: stand

stand:
    rgraph: "coin_beast_stand"
    hitbox:
        ;;   .   .
        ;;   *\*/*
        ;; . - + - .
        ;;   */*\*
        ;;  (.)  .

    if:
        coll: any no
            ;;  (+)- +
    then:
        move: 1 0
        goto immediate: start_jump

    if:
        key: isdown d
    then:
        goto delay: crouching

    if:
        key: isdown f
        coll: all yes
            ;;        +
            ;;       /
            ;; ( )  +
    then:
        move: 1 0
        rot: 1
        goto delay: step_up

    if:
        key: wasdown u
        coll: all no
            ;;       \*/*
            ;;        + -
            ;;       /*\*
            ;; ( )
    then:
        move: 1 0
        goto immediate: start_jump

    if:
        key: isdown f
        coll: all yes
            ;; ( )  + - +
        coll: all no
            ;;       \*/*
            ;;        + -
            ;;       /*\*
            ;; ( )
    then:
        move: 1 0
        goto delay: step

    if:
        key: isdown f
        coll: all yes
            ;; ( )  +
            ;;       \
            ;;        +
        coll: all no
            ;;       \*/*
            ;;        + -
            ;;       /*\*/*
            ;; ( )    - + -
            ;;        */*\*
    then:
        move: 1 0
        rot: -1
        goto delay: step_down

    if:
        key: isdown f
        coll: any yes
            ;;        +
            ;;       /
            ;; ( )
    then:
        move: 1 0
        rot: 1
        goto immediate: start_jump

    if:
        key: isdown f
        coll: all no
            ;;       \*/*
            ;;        + -
            ;;       /*\*
            ;; ( )
    then:
        move: 1 0
        goto immediate: start_jump

    if:
        key: isdown b
    then:
        move: 1 0
        turn
        goto delay: turn

turn:
    rgraph: "coin_beast_turn"
    hitbox:
        ;;   .   .
        ;;   *\*/*
        ;; . - + - .
        ;;   */*\*
        ;;  (.)  .
    if() then: goto immediate: stand

step:
    rgraph: "coin_beast_step"
    hitbox:
        ;;   .   .
        ;;   *\*/*
        ;; . - + - .
        ;;   */*\*
        ;;  (.)  .
    if() then: goto immediate: stand

step_up:
    rgraph: "coin_beast_step_up"
    hitbox:
        ;;   .   .
        ;;   *\*/*
        ;; . - + - .
        ;;   */*\*
        ;;  (.)  .
    if() then: goto immediate: stand

step_down:
    rgraph: "coin_beast_step_down"
    hitbox:
        ;;   .   .   .
        ;;   *\*/*\*/*
        ;; . - + - . - .
        ;;   */*\*/*\*
        ;;   .  (.)  .
    if() then: goto immediate: stand

appear_crouch:
    rgraph: "dead_once_crouch_reversed"
    if() then: goto: crouch

crouching:
    rgraph: "coin_beast_crouching"
    hitbox:
        ;;   .   .
        ;;   *\*/*
        ;; . - + - .
        ;;   */*\*
        ;;  (.)  .
    if() then: goto: crouch

crouch:
    rgraph: "coin_beast_crouch"
    hitbox:
        ;;     .
        ;;     *
        ;;  (.)  .

    if:
        key: not isdown d
    then:
        goto delay: uncrouching

    if:
        key: isdown b
    then:
        move: 1 0
        turn
        goto: crouch
        delay: 1

uncrouching:
    rgraph: "coin_beast_uncrouching"
    hitbox:
        ;;   .   .
        ;;   *\*/*
        ;; . - + - .
        ;;   */*\*
        ;;  (.)  .
    if() then: goto: stand

start_jump:
    if() then:
        goto: jump
        delay: 2

jump:
    rgraph: "coin_beast_jump"
    unsafe
    hitbox:
        ;;   .   .
        ;;   *\*/*
        ;; . - + - .
        ;;   */*\*
        ;;  (.)  .

    if:
        coll: all yes
            ;;        +
            ;;       /
            ;; ( )  +
    then:
        move: 1 0
        rot: 1
        goto delay: step_up

    if:
        coll: all yes
            ;; ( )  +
        coll: any yes
            ;;        +
            ;;       /
            ;; ( )
    then:
        move: 1 0
        rot: 1
        goto immediate: start_jump

    if:
        key: not isdown u
        coll: all no
            ;;       \*/*
            ;;        + -
            ;;       /*\*
            ;; ( )
        coll: all yes
            ;; ( )  + - +
    then:
        move: 1 0
        goto delay: step

    if:
        coll: all no
            ;;       \*/*
            ;;        + -
            ;;       /*\*
            ;; ( )
    then:
        move: 1 0
        goto immediate: start_jump

    if() then:
        goto delay: mostly_dead


collected:
    rgraph: "dead_once"
    unsafe

mostly_dead:
    rgraph: "dead_cycle"
    unsafe
    if() then: die mostly

dead:
    rgraph: "dead_cycle"
    unsafe
    if() then: die
