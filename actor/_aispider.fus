
# May be used for spider *or* coin_beast!

state "mainloop":
    # Main AI loop


    "always"
    if() then:
        key: up u
        key: up d
        key: up l
        key: up r
        key: up x
        key: up y
        continue


    "dead"
    if: dead()
    then:
        goto: "create_body"
        delay: 42


    "jumping"
    if: not:
        coll: all yes
            ;; (+)- +
    then:
        # If we're jumping, then do nothing...


    # Sometimes, jump!
    "jump"
    if:
        chance: 40%
        any:
            all:
                # Jump forward small, land forward
                coll: all yes
                    ;; ( )  .   .   + - +
                coll: any no
                    ;; ( )  . - + - .   .
                coll: all no
                    ;;       \*/*\*/*\*/*
                    ;;        + - + - + -
                    ;;       /*\*/*\*/*\*
                    ;; ( )  .   .   .   .
            all:
                # Jump forward small, land up
                coll: all yes
                    ;;                +
                    ;;               /
                    ;; ( )  .   .   +
                coll: any no
                    ;; ( )  . - + - .
                coll: all no
                    ;;       \*/*\*/*
                    ;;        + - + -
                    ;;       /*\*/*\*
                    ;; ( )  .   .   .
            all:
                # Jump forward medium, land forward
                coll: all yes
                    ;; ( )  .   .   .   + - +
                coll: any no
                    ;; ( )  . - + - + - .   .
                coll: all no
                    ;;       \*/*\*/*\*/*\*/*
                    ;;        + - + - + - + -
                    ;;       /*\*/*\*/*\*/*\*
                    ;; ( )  .   .   .   .   .
            all:
                # Jump forward medium, land up
                coll: all yes
                    ;;                    +
                    ;;                   /
                    ;; ( )  .   .   .   +
                coll: any no
                    ;; ( )  . - + - + - .
                coll: all no
                    ;;       \*/*\*/*\*/*
                    ;;        + - + - + -
                    ;;       /*\*/*\*/*\*
                    ;; ( )  .   .   .   .
    then:
        key: down u


    # Sometimes, crawl!
    "crawl"
    if:
        chance: 25%
        any:
            all:
                coll: any yes
                    # Only crawl somewhere we couldn't just walk
                    ;;       \*/*
                    ;;        + -
                    ;;
                    ;; ( )  .
                coll: all yes
                    ;; ( )  + - +
                coll: all no
                    ;;     \*/*
                    ;; (.)  .
            all:
                coll: any yes
                    # Only crawl somewhere we couldn't just walk
                    ;;       \*/*
                    ;;        + -
                    ;;         \*/*
                    ;; ( )  .   + -
                    ;;           \*
                coll: all yes
                    ;; ( )  +
                    ;;       \
                    ;;        +
                coll: all no
                    ;;     \*/*
                    ;; (.)  . -
                    ;;        *
    then:
        key: down d
        set myvar("crawl_time"): 0
        goto: "mainloop_crawl"


    "turn"
    if:
        # Small chance to spontaneously turn around...
        # Can help get un-stuck from certain situations
        # NOTE: multiple "chance" conditions can be used to achieve sub-1% chance...
        chance: 1%
        chance: 20%
    then:
        key: down b


    # Always walk straight ahead if we can
    "step"
    if: any:
        all:
            # Step forward
            coll: all yes
                ;; ( )  + - +
            coll: all no
                ;;       \*/*
                ;;        + -
                ;;       /*\*
                ;; ( )
        all:
            # Step forward over a small gap
            coll: all yes
                ;; ( )  .   + - +
            coll: all no
                ;;       \*/*\*/*
                ;;        + - + -
                ;;       /*\*/*\*
                ;; ( )  .   .   .
        all:
            # Step up
            coll: all yes
                ;;        +
                ;;       /
                ;; ( )  +
        all:
            # Step up over a small gap
            coll: all yes
                ;;            +
                ;;           /
                ;; ( )  .   +
            coll: all no
                ;;       \*/*
                ;;        + -
                ;;       /*\*
                ;; ( )
        all:
            # Step up over a small gap
            coll: all yes
                ;;          +
                ;;         /
                ;;        +
                ;;
                ;; ( )  +
            coll: all no
                ;;
                ;;    *\*/*
                ;;    - + - .
                ;;       \*
                ;;        .
                ;;
                ;; ( )  .
        all:
            # Small backflip
            coll: all yes
                ;;      +
                ;;       \
                ;;        +
                ;;
                ;; ( )  +
        all:
            # Medium backflip
            coll: all yes
                ;;  + - +
                ;;
                ;;        +
                ;;
                ;; ( )  +
        all:
            # Step down
            coll: all yes
                ;; ( )  +
                ;;       \
                ;;        +
            coll: all no
                ;;       \*/*
                ;;        + -
                ;;       /*\*/*
                ;; ( )    - + -
                ;;        */*\*
    then:
        key: down f


    # Turn around if we're stuck
    "turn_because_stuck"
    if() then:
        key: down b


state "mainloop_crawl":

    "always"
    if() then:
        key: up u
        #key: up d # Don't uncrawl by default!..
        key: up l
        key: up r
        key: up x
        key: up y

        inc myvar("crawl_time")
        continue


    # Sometimes, stand!
    "stand"
    if:
        expr: > myvar("crawl_time") 7 # Don't just stand up right after crouching...
        chance: 25%
        coll: all no
            ;;    *\*/*
            ;;    - + -
            ;;    */*\*
            ;;   ( )
    then:
        key: up d
        goto: "mainloop"


    # Always walk straight ahead if we can
    "step"
    if: any:
        all:
            coll: all yes
                ;;        +
                ;;       /
                ;; ( )  +
            coll: all no
                ;;     \*
                ;; ( )
        all:
            coll: all yes
                ;; ( )  + - +
            coll: all no
                ;;     \*/*
                ;; (.)  .
        all:
            coll: all yes
                ;; ( )  +
                ;;       \
                ;;        +
            coll: all no
                ;;     \*/*
                ;; (.)  . -
                ;;        *
    then:
        key: down f


    # Stand up if we're stuck
    "stand_because_stuck"
    if:
        chance: 70%
        coll: all no
            ;;    *\*/*
            ;;    - + -
            ;;    */*\*
            ;;   ( )
    then:
        key: up d
        goto: "mainloop"


    # Turn around if we're stuck
    "turn_because_stuck"
    if() then:
        key: down b
