
# May be used for spider *or* coin_beast!

state "mainloop":
    # Main AI loop

    "dead"
    if: dead()
    then:
        key: up u
        key: up d
        key: up l
        key: up r
        key: up x
        key: up y
        goto: "create_body"
        delay: 42


    "always"
    if() then:
        key: up b
        continue


    "wait_for_jump"
    if:
        key: isdown u
        coll: all yes
            ;; (+)- +
    then:
        # We're still on the ground, wait for our jump to start...


    "jumping"
    if: not:
        coll: all yes
            ;; (+)- +
    then:
        # If we're jumping, then do nothing...
        key: up u


    # Sometimes, jump!
    "jump"
    if:
        chance: 20%
        any:
            all:
                coll: all yes
                    ;; ( )  .   .   + - +
                coll: any no
                    ;; ( )  . - + - .   .
                coll: all no
                    ;;       \*/*\*/*\*/*
                    ;;        + - + - + -
                    ;;       /*\*/*\*/*\*
                    ;; ( )  .   .   .   .
            all:
                coll: all yes
                    ;;                +
                    ;;               /
                    ;; ( )  .   .   +
                coll: any no
                    ;; ( )  . - + - .
                coll: all no
                    ;;       \*/*\*/*
                    ;;        + - + -
                    ;;       /*\*/*\*
                    ;; ( )  .   .   .
            all:
                coll: all yes
                    ;; ( )  .   .   .   + - +
                coll: any no
                    ;; ( )  . - + - + - .   .
                coll: all no
                    ;;       \*/*\*/*\*/*\*/*
                    ;;        + - + - + - + -
                    ;;       /*\*/*\*/*\*/*\*
                    ;; ( )  .   .   .   .   .
            all:
                coll: all yes
                    ;;                    +
                    ;;                   /
                    ;; ( )  .   .   .   +
                coll: any no
                    ;; ( )  . - + - + - .
                coll: all no
                    ;;       \*/*\*/*\*/*
                    ;;        + - + - + -
                    ;;       /*\*/*\*/*\*
                    ;; ( )  .   .   .   .
    then:
        key: up f
        key: down u


    # Sometimes, crawl!
    "crawl"
    if:
        chance: 25%
        any:
            all:
                coll: any yes
                    # Only crawl somewhere we couldn't just walk
                    ;;       \*/*
                    ;;        + -
                    ;;
                    ;; ( )  .
                coll: all yes
                    ;; ( )  + - +
                coll: all no
                    ;;     \*/*
                    ;; (.)  .
            all:
                coll: any yes
                    # Only crawl somewhere we couldn't just walk
                    ;;       \*/*
                    ;;        + -
                    ;;         \*/*
                    ;; ( )  .   + -
                    ;;           \*
                coll: all yes
                    ;; ( )  +
                    ;;       \
                    ;;        +
                coll: all no
                    ;;     \*/*
                    ;; (.)  . -
                    ;;        *
    then:
        key: up f
        key: down d
        set myvar("crawl_time"): 0
        goto: "mainloop_crawl"


    "turn"
    if:
        # Small chance to spontaneously turn around...
        # Can help get un-stuck from certain situations
        # NOTE: multiple "chance" conditions can be used to achieve sub-1% chance...
        chance: 1%
        chance: 20%
    then:
        key: up f
        key: down b


    # Always walk straight ahead if we can
    "step"
    if: any:
        all:
            coll: all yes
                ;; ( )  + - +
            coll: all no
                ;;       \*/*
                ;;        + -
                ;;       /*\*
                ;; ( )
        all:
            coll: all yes
                ;;            +
                ;;           /
                ;; ( )  .   +
            coll: all no
                ;;       \*/*
                ;;        + -
                ;;       /*\*
                ;; ( )
        all:
            coll: all yes
                ;;        +
                ;;       /
                ;; ( )  +
        all:
            coll: all yes
                ;;      +
                ;;       \
                ;;        +
                ;;
                ;; ( )  +
        all:
            coll: all yes
                ;;  + - +
                ;;
                ;;        +
                ;;
                ;; ( )  +
        all:
            coll: all yes
                ;;          +
                ;;         /
                ;;        +
                ;;
                ;; ( )  +
            coll: all no
                ;;
                ;;    *\*/*
                ;;    - + - .
                ;;       \*
                ;;        .
                ;;
                ;; ( )  .
        all:
            coll: all yes
                ;; ( )  +
                ;;       \
                ;;        +
            coll: all no
                ;;       \*/*
                ;;        + -
                ;;       /*\*/*
                ;; ( )    - + -
                ;;        */*\*
    then:
        key: down f


    # Turn around if we're stuck
    "turn_because_stuck"
    if() then:
        key: up f
        key: down b


state "mainloop_crawl":

    "always"
    if() then:
        key: up b
        inc myvar("crawl_time")
        continue


    # Sometimes, stand!
    "stand"
    if:
        expr: > myvar("crawl_time") 7 # Don't just stand up right after crouching...
        chance: 25%
        coll: all no
            ;;    *\*/*
            ;;    - + -
            ;;    */*\*
            ;;   ( )
    then:
        key: up f
        key: up d
        goto: "mainloop"


    # Always walk straight ahead if we can
    "step"
    if: any:
        all:
            coll: all yes
                ;;        +
                ;;       /
                ;; ( )  +
            coll: all no
                ;;     \*
                ;; ( )
        all:
            coll: all yes
                ;; ( )  + - +
            coll: all no
                ;;     \*/*
                ;; (.)  .
        all:
            coll: all yes
                ;; ( )  +
                ;;       \
                ;;        +
            coll: all no
                ;;     \*/*
                ;; (.)  . -
                ;;        *
    then:
        key: down f


    # Stand up if we're stuck
    "stand_because_stuck"
    if:
        chance: 70%
        coll: all no
            ;;    *\*/*
            ;;    - + -
            ;;    */*\*
            ;;   ( )
    then:
        key: up f
        key: up d
        goto: "mainloop"


    # Turn around if we're stuck
    "turn_because_stuck"
    if() then:
        key: up f
        key: down b
